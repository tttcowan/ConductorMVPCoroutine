apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def appName = "App Sauce"

configurations {
    ktlint
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.appsauce.mvpappsauce"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue("string", "app_name", "${appName}")
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
    compileOptions {
        targetCompatibility = "8"
        sourceCompatibility = "8"
    }

}

//For parcelable
androidExtensions {
    experimental = true
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.google.android.material:material:$support_lib_version"

    //Blueline Conductor
    implementation 'com.bluelinelabs:conductor:2.1.5'

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    //OkHttp
    implementation 'com.squareup.okhttp3:okhttp:4.4.1'

    //Stetho
    implementation "com.facebook.stetho:stetho:1.5.1"
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'

    //Multidex
    implementation 'androidx.multidex:multidex:2.0.1'

    testImplementation 'junit:junit:4.13'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha04'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha04'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    //Static analysis
    ktlint "com.github.shyiko:ktlint:0.31.0"

    //Kotlinx
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.4'

}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlin.Experimental"]
}
